#!/usr/bin/env perl
use strict;
use warnings;

use Number::Bytes::Human qw(format_bytes);

use constant TRUE => !! 1;
use constant FALSE => !! '';

my $ZFS = '/sbin/zfs';

sub percent_string($$$) {
    my ($val, $max_val, $max_len) = @_;
    return '' unless $max_val;
    return '#' x ($val * $max_len / $max_val);
}

sub read_volumes() {
    my @volumes = ();
    
    open my $fh, '-|', "$ZFS list -o name,usedsnap -H -p" or die "can't call $ZFS: $!\n";
    while (my $line = <$fh>) {
	chomp $line;
	my ($name, $size) = split /\s+/, $line, 2;
	next unless defined $size;
	
	my $pool = $name;
	$pool =~ s|/.*$||;

	next if $pool eq $name;

	push @volumes, {
	    NAME => $name,
	    POOL => $pool,
	    SIZE => $size,
	};
    }
    close $fh or die "can't close $ZFS: $!\n";
    
    return @volumes;
}

sub read_snapshots($) {
    my ($volume) = @_;
    my @snapshots = ();
    
    open my $fh, '-|', "$ZFS list -o name,used,creation -t snapshot -H -p $volume" or die "can't call $ZFS: $!\n";
    while (my $line = <$fh>) {
	chomp $line;
	my ($name, $size, $date) = split /\s+/, $line, 3;
	next unless defined $date;
	
	push @snapshots, {
	    NAME => $name,
	    SIZE => $size,
	    DATE => $date,
	};
    }
    close $fh or die "can't close $ZFS: $!\n";
    
    return @snapshots;
}

sub is_contained($$@) {
    my ($field, $value, @list) = @_;
    foreach my $element (@list) {
	if ($element->{$field} eq $value) {
	    return TRUE;
	}
    }
    return FALSE;
}

sub filter_by_pool($@) {
    my $pool = shift;
    return grep { $_->{POOL} eq $pool } @_;
}

sub sort_by_size(@) {
    return sort { $a->{SIZE} <=> $b->{SIZE} } @_;
}

sub sort_by_date(@) {
    return sort { $b->{DATE} <=> $a->{DATE} } @_;
}

sub sum_size(@) {
    my $total_size = 0;
    $total_size += $_->{SIZE} foreach @_;
    return $total_size;
}

sub show_volumes(@) {
    my (@volumes) = @_;

    @volumes = sort_by_size @volumes;
    my $total_size = sum_size @volumes;
    my $max_size = $volumes[-1]->{SIZE};

    foreach my $volume (@volumes) {
	my $name = $volume->{NAME};
	my $size = $volume->{SIZE};
	my $histogram = percent_string($size, $max_size, 16);
	my $size_percent = $size * 100 / $total_size;
	my $size_readable = format_bytes( $size );
	printf "%16s  %3.0f%%  %4s  %s\n", $histogram, $size_percent, $size_readable, $name;
    }
}

sub show_snapshots($) {
    my ($volume) = @_;

    my @snapshots = read_snapshots $volume;
    @snapshots = sort_by_date @snapshots;
    my $total_size = sum_size @snapshots;
    my $max_size = (sort_by_size @snapshots)[-1]->{SIZE};
    
    foreach my $snapshot (@snapshots) {
	my $name = $snapshot->{NAME};
	my $size = $snapshot->{SIZE};
	my $date = $snapshot->{DATE};
	my $histogram = percent_string($size, $max_size, 16);
	my $size_percent = $total_size
	    ? sprintf "%3.0f%%", $size * 100 / $total_size
	    : '  - ';
	my $size_readable = format_bytes( $size );
	printf "%16s  %s  %4s  %s\n", $histogram, $size_percent, $size_readable, $name;
    }
}

sub show_help() {
    print <<EOF
usage:
  list-zfs-snapshots              -  list total snapshot usage of all volumes
  list-zfs-snapshots <pool>       -  list total snapshot usage of all volumes in <pool>
  list-zfs-snapshots <volume>     -  list detailled snapshot usage in <volume>
  list-zfs-snapshots -h | --help  -  show this help
EOF
    ;
}

my @volumes = read_volumes;
my $command = shift @ARGV;

if (! defined $command or $command eq '') {
    show_volumes @volumes;
}
elsif ($command eq '-h' or $command eq '--help') {
    show_help;
}
elsif (is_contained 'POOL', $command, @volumes) {
    my $pool = $command;
    @volumes = filter_by_pool $pool, @volumes;
    show_volumes @volumes;
}
elsif (is_contained 'NAME', $command, @volumes) {
    my $volume = $command;
    show_snapshots $volume;
}
else {
    die "<$command> is neither a known pool nor a known volume\n";
}
